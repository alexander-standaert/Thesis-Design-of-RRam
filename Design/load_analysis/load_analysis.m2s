///////////////////////////////////////////////////////////////////////////
// SIMULATION SETTINGS

simulator lang=spice
$if param.simulationtype == 'tran' 
.lib '../../technology_models/tech_wrapper.lib' tt
$else
.include '../../technology_models/monte_carlo_models.scs'
$end

.OPTIONS METHOD=trap
.TEMP $<param.temp>$

simulator lang=spectre

$if param.simulationtype == 'tran' 
    ana tran step=$<param.steptime>$ stop=$<param.stoptime>$
    parameters tp = MOSP
    parameters tn = MOSN
    parameters Rmemcellhh = $<param.Rmemcellhh>$
    parameters Rmemcellhl = $<param.Rmemcellhl>$
    parameters Rmemcelllh = $<param.Rmemcelllh>$
    parameters Rmemcellll = $<param.Rmemcellll>$
$else
    mc montecarlo donominal=no variations=mismatch seed=1234 numruns=$<param.mcruns>$ firstrun=1 saveprocessparams=yes savefamilyplots=yes {
        ana tran step=$<param.steptime>$ stop=$<param.stoptime>$
    }
    parameters tp = mc_pmos_lvt
    parameters tn = mc_nmos_lvt
    parameters enableVtMismatch=$<param.VtMismatch>$
    parameters enableBMismatch=$<param.BMismatch>$
    parameters Rmemcellhh = mismatched_Rhigh
    parameters Rmemcellhl = mismatched_Rhigh
    parameters Rmemcelllh = mismatched_Rlow
    parameters Rmemcellll = mismatched_Rlow
    
$end

///////////////////////////////////////////////////////////////////////////
// TEST BENCH + SIGNALS
mmemarray_switch ( bl_switch wl_1 wl_2 wl_3 wl_4 sel_sl sel_pulldown gnd vdd) memarray
mmemarray_bias ( bl_bias wl_1 wl_2 wl_3 wl_4 sel_sl sel_pulldown gnd vdd) memarray
mmemarray_diode ( bl_diode wl_1 wl_2 wl_3 wl_4 sel_sl sel_pulldown gnd vdd) memarray
mmemarray_bulk ( bl_bulk wl_1 wl_2 wl_3 wl_4 sel_sl sel_pulldown gnd vdd) memarray

mload_switch ( bl_switch sel_load gnd vdd) load_switch
mload_bias ( bl_bias sel_load sel_bias gnd vdd) load_bias
mload_diode ( bl_diode sel_load gnd vdd) load_diode
mload_bulk ( bl_bulk sel_load sel_bulk gnd vdd) load_bulk

vwl_1 wl_1 gnd vsource type=pwl wave=$<param.wl_1>$
vwl_2 wl_2 gnd vsource type=pwl wave=$<param.wl_2>$
vwl_3 wl_3 gnd vsource type=pwl wave=$<param.wl_3>$
vwl_4 wl_4 gnd vsource type=pwl wave=$<param.wl_4>$

vsel_pulldown sel_pulldown gnd vsource type=pwl wave=$<param.sel_pulldown>$
vsel_load sel_load gnd vsource type=pwl wave=$<param.sel_load>$
vsel_bias sel_bias gnd vsource type=pwl wave=$<param.sel_bias>$
vsel_bulk sel_bulk gnd vsource type=pwl wave=$<param.sel_bulk>$
vsel_sl sel_sl gnd vsource type=pwl wave=$<param.sel_sl>$

vgnd gnd 0 vsource type=dc dc=0
vvdd vdd 0 vsource type=pwl wave=[0 0 0.01n 1]


///////////////////////////////////////////////////////////////////////////
// MEMARRAY

subckt memarray ( bl wl_1 wl_2 wl_3 wl_4 sel_sl sel_pulldown gnd vdd)

// mem cell high high
rmemcell_hh (bl nodecell_hh) resistor r=Rmemcellhh
mcell_hh nodecell_hh wl_1 sl gnd tn
// mem cell high low
rmemcell_hl (bl nodecell_hl) resistor r=Rmemcellhl
mcell_hl nodecell_hl wl_2 sl gnd tn

// mem cell low high
rmemcell_lh (bl nodecell_lh) resistor r=Rmemcelllh
mcell_lh nodecell_lh wl_3 sl gnd tn
// mem cell low low
rmemcell_ll (bl nodecell_ll) resistor r=Rmemcellll
mcell_ll nodecell_ll wl_4 sl gnd tn

// bl
cbl (bl gnd) capacitor c=$<param.Cbl>$
mpulldown gnd sel_pulldown bl gnd tn 

// sl
msl gnd sel_sl sl gnd tn w=$<param.wsl>$
csl (sl gnd) capacitor c=$<param.Csl>$

ic bl=$<param.init_bl>$

ends memarray

///////////////////////////////////////////////////////////////////////////
// LOADS


subckt load_switch ( bl sel_load gnd vdd)
mswitch vdd sel_load bl vdd tp w=$<param.wswitchswitch>$
ends load_switch

subckt load_bias ( bl sel_load sel_bias gnd vdd)
mswitch vdd sel_load bl0 vdd tp w=$<param.wswitchbias>$
mbias bl0 sel_bias bl vdd tp w=$<param.wbias>$
ends load_bias

subckt load_diode ( bl sel_load gnd vdd)
mswitch vdd sel_load bl0 vdd tp w=$<param.wswitchdiode>$
mdiode bl0 bl bl vdd tp w=$<param.wdiode>$
ends load_diode

subckt load_bulk ( bl sel_load sel_bias gnd vdd)
mswitch vdd sel_load bl0 vdd tp w=$<param.wswitchbulk>$
mbulk bl0 sel_bias bl bl tp w=$<param.wbulk>$
ends load_bulk
